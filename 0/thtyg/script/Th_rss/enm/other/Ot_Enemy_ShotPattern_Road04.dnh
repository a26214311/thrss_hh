task Road04_ShotPattern01( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 60;
	let wt		= 80;
	let speed	= 0;
	let acc		= 0.025;
	let mx		= 2;
	let delay	= 9;
	let ave		= 1;
	let seal	= 72;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		wt = 300;
		acc = 0.005;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		swt = 35;
		wt = 30;
		mx = 5;
		ave = 1;
	}
	case( "Lunatic" ){
		swt = 0;
		wt = 10;
		mx = 5;
		ave = 6;
	}
	case( "OverDrive" ){
		swt = 0;
		wt = 30;
		speed = -2;
		acc = 0.05;
		mx = 5;
		ave = 6;
	}

	loop( swt ){ yield; }

	while( !Obj_IsDeleted( objID ) ){
		let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
		let angle = round( GetAngleToPlayer( objID ) / ave ) * ave;
		let distance = GetObjectDistance( objID, GetPlayerObjectID );
//		if( distance < seal ){ break; }
		if( pos[0]>MinX&&pos[0]<MaxX&&pos[1]>MinY&&pos[1]<MaxY-64 && distance > seal ){
			let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
			NotifyEventAll( EV_USER + 40, [ sht, delay ] );
			ObjSound_Play( CreateShotSE[ 0 ] );
		}
		loop( wt ){ yield; }
	}
}

task Road04_ShotPattern02A( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 100;
	let wtA		= 10;
	let wtB		= 60;
	let speed	= 0;
	let acc		= 0.05;
	let mx		= 3.4;
	let delay	= 12;

	let lp		= 6;
	let l		= 3;

	let wp		= 6;
	let roundway= 1;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		lp = 2;
		wtB = 100;
	}
	case( "Normal" ){
		lp = 4;
		mx = 2.5;
		wtB = 80;
	}
	case( "Hard" ){
		wp = 9;
		roundway = 3;
	}
	case( "Lunatic" ){
		wp = 9;
		roundway = 5;
	}
	case( "OverDrive" ){
		wp = 6;
		roundway = 7;
		wtB = 60;
		l = 5;
	}

	loop( swt ){ yield; }

	loop( l ){
		let p = GetAngleToPlayer( objID );
		let way		= 2;
		let wide	= wp;
		loop( lp ){
			if( Obj_IsDeleted( objID ) ){ return; }
			let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
			let ag = p;
			loop( roundway ){
				let angle = ag - wide / 2;
				loop( way ){
					let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
					NotifyEventAll( EV_USER + 40, [ sht, delay ] );
					angle += wide / ( way - 1 );
				}
				ag += 360 / roundway;
			}
			ObjSound_Play( CreateShotSE[ 1 ] );
			way += 1;
			wide += wp;
			loop( wtA ){ yield; }
		}
		loop( wtB ){ yield; }
	}
}

task Road04_ShotPattern02B( objID, master, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 150;
	let wtA		= 5;
	let wtB		= 90;
	let speed	= 0;
	let acc		= 0.05;
	let mx		= 3.4;
	let delay	= 12;

	let lp		= 6;
	let l		= 3;
	let rd		= 2;

	let way		= 2;
	let wide	= 0;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		lp = 2;
		mx = 2;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		wide = 18;
	}
	case( "Lunatic" ){
		way = 5;
		wide = 9;
	}
	case( "OverDrive" ){
		way = 3;
		wide = 24;
		wtB = 60;
		l = 5;
	}

	loop( swt ){ yield; }

	loop( l ){
		let p = atan2( ObjMove_GetY( objID ) - ObjMove_GetY( master ), ObjMove_GetX( objID ) - ObjMove_GetX( master ) );

		loop( lp ){
			if( Obj_IsDeleted( objID ) ){ return; }
			let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
			let angle = p - wide / 2 + round( rand( -1, 1 ) ) * rd;;
			loop( way ){
				let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
				NotifyEventAll( EV_USER + 40, [ sht, delay ] );
				angle += wide / ( way - 1 );
			}
			ObjSound_Play( CreateShotSE[ 0 ] );
			loop( wtA ){ yield; }
		}
		loop( wtB ){ yield; }
	}
}

task Road04_ShotPattern03( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 70;
	let wtA		= 3;
	let wtB		= 70;
	let speed	= 3.5;
	let acc		= -0.1;
	let mx		= 1.4;
	let delay	= 12;

	let lp		= 2;
	let rd		= 0;

	let way		= 3;
	let wide	= 30;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		lp = 3;
		way = 2;
		wide = 0;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		way = 3;
		wtB = 40;
		wide = 45;
		lp = 4;
	}
	case( "Lunatic" ){
		way = 3;
		wtB = 15;
		wide = 60;
		lp = 4;
	}
	case( "OverDrive" ){
		lp = 2;
		way = 3;
		wide = 70;
		wtB = 15;
	}
	local{

	}

	loop( swt ){ yield; }

	while( !Obj_IsDeleted( objID ) ){
		let p = GetAngleToPlayer( objID );
		loop( lp ){
			if( Obj_IsDeleted( objID ) ){ return; }
			
			let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
			if( pos[0]>MinX&&pos[0]<MaxX&&pos[1]>MinY&&pos[1]<MaxY-64 ){
				let angle = p - wide / 2 + round( rand( -1, 1 ) ) * rd;;
				loop( way ){
					let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
					NotifyEventAll( EV_USER + 40, [ sht, delay ] );
					angle += wide / ( way - 1 );
				}
				ObjSound_Play( CreateShotSE[ 0 ] );
			}
			loop( wtA ){ yield; }
		}
		loop( wtB ){ yield; }
	}
}

task Road04_ShotPattern04( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 200;
	let wt		= 24;
	let speed	= 4;
	let acc		= -0.1;
	let mx		= 1;
	let delay	= 9;
	let rd		= 10;
	let rv		= 1;
	let seal	= 88;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		wt = 90;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		wt = 10;
		rd = 25;
	}
	case( "Lunatic" ){
		wt = 10;
		rd = 12;
		rv = 3;
		mx = 2;
	}
	case( "OverDrive" ){
		wt = 5;
		rd = 72;
		rv = 2;
	}
	local{

	}
	

	loop( swt ){ yield; }

	while( !Obj_IsDeleted( objID ) ){
		let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
		let angle = GetAngleToPlayer( objID ) + round( rand( -rv, rv ) ) * rd;
		let distance = GetObjectDistance( objID, GetPlayerObjectID );
//		if( distance < seal ){ break; }
		if( pos[0]>MinX&&pos[0]<MaxX&&pos[1]>MinY&&pos[1]<MaxY-96 && distance > seal ){
			let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
			NotifyEventAll( EV_USER + 40, [ sht, delay ] );
			ObjSound_Play( CreateShotSE[ 0 ] );
		}
		loop( wt ){ yield; }
	}
}

task Road04_ShotPattern05A( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 40;
	let wtA		= 5;
	let wtB		= 30;
	let spd		= [3,2];
	let acc		= 0;
	
	let delay	= 12;

	let lp		= 8;

	let way		= 11;
	let wide	= 180;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		lp = 6;
		way = 5;
		spd = [2];
	}
	case( "Normal" ){
		way = 11;
		spd = [2,3];
	}
	case( "Hard" ){
		way = 17;
	}
	case( "Lunatic" ){
		way = 17;
		spd = [2.7,3,5];
	}
	case( "OverDrive" ){
		way = 11;
		spd = [ 1, 2.2, 6 ];
	}

	loop( swt ){ yield; }
	
	let mx		= spd;
	loop( 3 ){
		let px = ObjMove_GetX( GetPlayerObjectID );
		let py = ObjMove_GetY( GetPlayerObjectID );
		let ex = ObjMove_GetX( objID );
		let ey = ObjMove_GetY( objID );

		loop( lp ){
			if( Obj_IsDeleted( objID ) ){ return; }
			let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
			let vx = [ - 30, 30 ];
			ascent( idx in 0..length( vx ) ){
				let p = atan2( py - ey, px - ( ex + vx[idx] ) );
				let angle = p - wide / 2;
				let s = 0;
				loop( way ){
					ascent( sd in 0..length( spd ) ){
						let speed = [ spd[sd], spd[sd]/1.75 ];
						let sht = CreateShotA2( pos[0] + vx[idx], pos[ 1 ], speed[s], angle, acc, mx[sd], shote, delay ); 
						NotifyEventAll( EV_USER + 40, [ sht, delay ] );
						angle += wide / ( way - 1 );
					}
					s = (s+1) % 2;
				}
			}
			ObjSound_Play( CreateShotSE[ 0 ] );
			loop( wtA ){ yield; }
		}
		loop( wtB ){ yield; }
	}
}


task Road04_ShotPattern05B( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 220;
	let wt		= 1;
	let speed	= [3];
	let sp		= 0;
	let acc		= 0;
	
	let delay	= 4;

	let wp		= 3;
	let wd		= 220;
	let wide 	= 150;
	let way		= 5;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		wt = 2;
		wp = 6;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		speed = [3,5];
	}
	case( "Lunatic" ){
		speed = [1.25,5];
	}
	case( "OverDrive" ){
		swt = 120;
		speed = [ 1.25, 5 ];
	}
	local{
		
	}

	loop( swt ){ yield; }

	while( wd <= 540 ){
		if( Obj_IsDeleted( objID ) ){ return; }
		let p = 90;
		let mx = speed;
		let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
		let ag = p - wd / 2;
		
		loop( 2 ){
			let angle = ag - wide / 2;
			loop( way ){
				let v = 18;
				let sx = pos[0] + v * cos( angle );
				let sy = pos[1] + v * sin( angle );
				ascent( idx in 0..length( speed ) ){
					let sht = CreateShotA2( sx, sy, speed[idx], angle, acc, mx[idx], shote, delay ); 
					NotifyEventAll( EV_USER + 40, [ sht, delay ] );
				}
				angle += wide / ( way - 1 );
			}
			ag += wd;
		}
		ObjSound_Play( CreateShotSE[ 0 ] );
		wd += wp;
		loop( wt ){ yield; }
	}
}

task Road04_ShotPattern05C( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 300;
	let wt		= 15;
	let spd		= 1;
	let acc		= 0;
	let mx		= 10;
	let delay	= 12;

	let lp		= 15;

	let way		= 2;
	let wide	= 0;
	let agp		= 9;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		lp = 9;
		agp = 16;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		wide = 24;
	}
	case( "Lunatic" ){
		way = 3;
		wide = 60;
	}
	case( "OverDrive" ){
		swt = 220;
		way = 3;
		wide = 60;
	}

	loop( swt ){ yield; }
	let r = 1;
	if( ObjMove_GetX( objID ) < CenX ){ r = -r; }
	loop( 4 ){
		if( Obj_IsDeleted( objID ) ){ return; }
		let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
		let p = GetAngleToPlayer( objID ) - 20 * r;
		let speed = spd;
		loop( lp ){
			let angle = p - wide / 2;
			loop( way ){
				let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
				NotifyEventAll( EV_USER + 40, [ sht, delay ] );
				angle += wide / ( way - 1 );
			}
			p += agp * r;
			speed *= 1.15;
			ObjSound_Play( CreateShotSE[ 1 ] );
		}
		r = -r;
		loop( wt ){ yield; }
	}
}

task Road04_ShotPattern06( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 60;
	let wt		= 60;
	let speed	= 0;
	let acc		= 0.05;
	let mx		= 3;
	let delay	= 9;
	let rd		= 10;
	let rv		= 1;
	let seal	= 72;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		swt = 120;
		wt = 90;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		swt = 40;
		wt = 40;
		rd = 30;
	}
	case( "Lunatic" ){
		swt = 10;
		wt = 16;
		rd = 18;
		rv = 3;
		mx = 1.8;
	}
	case( "OverDrive" ){
		swt = 10;
		wt = 20;
		rd = 8;
		rv = 5;
		speed = 4;
		acc = -0.05;
		mx = 1;
	}
	local{

	}
	

	loop( swt ){ yield; }

	while( !Obj_IsDeleted( objID ) ){
		let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
		let angle = GetAngleToPlayer( objID ) + round( rand( -rv, rv ) ) * rd;
		let distance = GetObjectDistance( objID, GetPlayerObjectID );
//		if( distance < seal ){ break; }
		if( pos[0]>MinX&&pos[0]<MaxX&&pos[1]>MinY&&pos[1]<MaxY-64 && distance > seal ){
			let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
			NotifyEventAll( EV_USER + 40, [ sht, delay ] );
			ObjSound_Play( CreateShotSE[ 0 ] );
		}
		loop( wt ){ yield; }
	}
}

task Road04_ShotPattern07( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 150 + rand( 0, 3 ) * 15;
	let wt		= 70;
	let speed	= 0;
	let acc		= 0.025;
	let mx		= 2.5;
	let delay	= 9;
	let rd		= 10;
	let rv		= 1;
	let lmy		= 96;
	let seal	= 80;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		wt = 120;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		wt = 18;
		rd = 25;
	}
	case( "Lunatic" ){
		wt = 10;
		rd = 18;
		rv = 2;
	}
	case( "OverDrive" ){
		wt = 16;
		rd = 6;
		rv = 2;
		lmy = 72;
	}
	local{

	}
	

	loop( swt ){ yield; }

	while( !Obj_IsDeleted( objID ) ){
		let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
		let angle = GetAngleToPlayer( objID ) + round( rand( -rv, rv ) ) * rd;
		let distance = GetObjectDistance( objID, GetPlayerObjectID );
		if( pos[0]>MinX&&pos[0]<MaxX&&pos[1]>MinY&&pos[1]<MaxY-lmy && distance > seal ){
			let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
			NotifyEventAll( EV_USER + 40, [ sht, delay ] );
			ObjSound_Play( CreateShotSE[ 1 ] );
		}
		loop( wt ){ yield; }
	}
}

task Road04_ShotPattern08( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 75;
	let wt		= 90;
	let speed	= 2;
	let acc		= 0;
	let mx		= 5;
	let delay	= 9;
	let ave		= 1;
	let seal	= 72;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		wt = 300;
		swt = 160;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		wt = 70;
		ave = 8;
	}
	case( "Lunatic" ){
		wt = 45;
		speed = 0;
		acc = 0.05;
		ave = 6;
	}
	case( "OverDrive" ){
		wt = 40;
		speed = 5;
		acc = -0.1;
		mx = 1.5;
		ave = 6;
	}

	loop( swt ){ yield; }

	while( !Obj_IsDeleted( objID ) ){
		let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
		let angle = round( GetAngleToPlayer( objID ) / ave ) * ave;
		let distance = GetObjectDistance( objID, GetPlayerObjectID );
//		if( distance < seal ){ break; }
		if( pos[0]>MinX&&pos[0]<MaxX&&pos[1]>MinY&&pos[1]<MaxY-64 && distance > seal ){
			let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
			NotifyEventAll( EV_USER + 40, [ sht, delay ] );
			ObjSound_Play( CreateShotSE[ 0 ] );
		}
		loop( wt ){ yield; }
	}
}

task Road04_ShotPattern09A( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 80;
	let wt		= 80;
	let speed	= 1;
	let acc		= 0.05;
	let mx		= 3;
	let delay	= 16;
	let roundway= 3;
	let way		= 2;
	let wide	= 20;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		wt = 150;
		mx = 2;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		roundway = 4;
		way = 3;
		wide = 15;
		mx = 5;
	}
	case( "Lunatic" ){
		roundway = 5;
		way = 3;
		wide = 16;
		mx = 5;
	}
	case( "OverDrive" ){
		roundway = 6;
		mx = 5;
	}
	local{

	}

	loop( swt ){ yield; }

	while( !Obj_IsDeleted( objID ) ){
		let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
		let p = GetAngleToPlayer( objID );
		if( pos[0]>MinX&&pos[0]<MaxX&&pos[1]>MinY&&pos[1]<MaxY-64 ){
			let ag = p;
			loop( roundway ){
				let angle = ag - wide / 2;
				loop( way ){
					let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
					NotifyEventAll( EV_USER + 40, [ sht, delay ] );
					angle += wide / ( way - 1 );
				}
				ag += 360 / roundway;
			}
			ObjSound_Play( CreateShotSE[ 1 ] );
		}
		loop( wt ){ yield; }
	}
}

task Road04_ShotPattern09B( objID, master, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 100;
	let wtA		= 5;
	let wtB		= 90;
	let speed	= 0;
	let acc		= 0.05;
	let mx		= 3.4;
	let delay	= 12;

	let lp		= 6;
	let rd		= 0;

	let way		= 2;
	let wide	= 10;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		lp = 2;
		mx = 2;
		wide = 0;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		wide = 25;
		rd = 1;
		wtB = 70;
	}
	case( "Lunatic" ){
		way = 2;
		wide = 24;
		rd = 10;
		wtB = 60;
	}
	case( "OverDrive" ){
		way = 3;
		wide = 18;
		rd = 0.5;
		wtB = 60;
	}

	loop( swt ){ yield; }

	loop( 3 ){
		let p = atan2( ObjMove_GetY( objID ) - ObjMove_GetY( master ), ObjMove_GetX( objID ) - ObjMove_GetX( master ) );
		p = - p;

		loop( lp ){
			if( Obj_IsDeleted( objID ) ){ return; }
			let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
			let angle = p - wide / 2 + round( rand( -1, 1 ) ) * rd;
			loop( way ){
				let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
				NotifyEventAll( EV_USER + 40, [ sht, delay ] );
				angle += wide / ( way - 1 );
			}
			ObjSound_Play( CreateShotSE[ 0 ] );
			loop( wtA ){ yield; }
		}
		loop( wtB ){ yield; }
	}
}

task Road04_ShotPattern10( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );
	
	let swt 	= 50;
	let wt		= 70;
	let speed	= 4;
	let acc		= -0.1;
	let mx		= 1.5;
	let delay	= 9;
	let ave		= 8;
	let seal	= 88;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		wt = 300;
	}
	case( "Normal" ){
	}
	case( "Hard" ){
		swt = 35;
		wt = 40;
		mx = 1;
	}
	case( "Lunatic" ){
		swt = 0;
		wt = 20;
		mx = 1;
		ave = 10;
	}
	case( "OverDrive" ){
		swt = 0;
		wt = 24;
		mx = 3;
		ave = 8;
	}

	loop( swt ){ yield; }

	while( !Obj_IsDeleted( objID ) ){
		let pos = [ ObjMove_GetX( objID ), ObjMove_GetY( objID ) ];
		let angle = round( GetAngleToPlayer( objID ) / ave ) * ave;
		let distance = GetObjectDistance( objID, GetPlayerObjectID );
		if( pos[0]>MinX&&pos[0]<MaxX&&pos[1]>MinY&&pos[1]<MaxY-96 && distance > seal ){
			let sht = CreateShotA2( pos[ 0 ], pos[ 1 ], speed, angle, acc, mx, shote, delay ); 
			NotifyEventAll( EV_USER + 40, [ sht, delay ] );
			ObjSound_Play( CreateShotSE[ 0 ] );
		}
		loop( wt ){ yield; }
	}
}


task Road04_ShotPattern11( objID, shote ){
	let GetDifficulty = GetAreaCommonData("System", "Difficulty", "NULL" );

	let speed	= 1;
	let angle	= GetAngleToPlayer( objID );
	let acc		= 0.025;
	let mx		= 5;
	let delay	= 16;
	
	let roundway= 1;
	let row		= 1;
	let ave		= 0;

	let seal	= 72;

	let distance= 0;
	
	alternative( GetDifficulty )
	case( "Easy" ){
		roundway = 1;
	}
	case( "Normal" ){
		roundway = 8;
		row = 3;
	}
	case( "Hard" ){
		roundway = 12;
		row = 3;
		ave = 3;
	}
	case( "Lunatic" ){
		roundway = 12;
		row = 7;
		ave = 7;
	}
	
	let pos		= [ ObjMove_GetX(objID), ObjMove_GetY(objID) ];
	if( roundway > 0 ){
		//　敵削除待ち
		while( !Obj_IsDeleted(objID) ){
			pos = [ ObjMove_GetX(objID), ObjMove_GetY(objID) ];
			angle = GetAngleToPlayer( objID );
			distance = GetObjectDistance( objID, GetPlayerObjectID );
			yield;
		}
		
		let dis = [0,0];
		if( pos[0] < MaxX+dis[0] && pos[0] > MinX-dis[0] && pos[1] < MaxY+dis[1] && pos[1] > MinY-dis[1] && distance > seal ){
			let ag = angle;
			loop( roundway ){
				ascent( let i in 0..(row) ){
					let sht = CreateShotA2( pos[0], pos[1], speed, ag + round( rand( -ave, ave ) ), acc, mx, shote, delay + i*2 );
					NotifyEventAll( EV_USER + 40, [ sht, delay + i*2 ] );
				}
				ag += 360 / roundway;
			}
			ObjSound_Play( CreateShotSE[1] );
		}
	}
}
