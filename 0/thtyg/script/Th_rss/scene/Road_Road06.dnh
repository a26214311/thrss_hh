function Road06(){
	let enm_num = GetEnemyIntersectionPosition(CenX, CenY, 512 );
	let plural;
	let compile_file_h=[0,0];
	
	plural		= [csd ~ "./enm/Plural_stage06A.dnh",
					csd ~ "./enm/Plural_stage06B.dnh"];
	

	
	
	
	
	//　nフレーム待機
	function FrameWait( n ){
		loop( n ){ yield };
	}
	//　自機ボム終了まで待機
	function WaitForSpell(){
		while( IsPlayerSpellActive() ){ yield; }
	}
	//　敵の数が 0 になるまで待機
	function WaitForZeroEnemy(){
		let AllEnemy	= GetAllEnemyID();
		let AllBoss		= GetEnemyBossObjectID();
		let AllScene	= GetEnemyBossSceneObjectID();
		while( length(AllEnemy) > 0 || length(enm_num) != 0 || length(AllBoss)!=0 || AllScene!=ID_INVALID ){
			AllEnemy	= GetAllEnemyID();
			AllBoss		= GetEnemyBossObjectID();
			AllScene	= GetEnemyBossSceneObjectID();
			yield;
		}
	}
	//　すべての敵を削除
	function VanishAllEnemy(){
		let AllEnemy	= GetAllEnemyID();
		let i			= 0;
		while(i<length(AllEnemy)){
			Obj_Delete(AllEnemy[i]);
			i++;
		}
	}
	
	//　ざこてきてすとでーた
	/*
	local{
		CreateEnemy_Road06_2( [MaxX+32,MinY+112], [MinX+48,MinY+80], [500,200], 180, 0.5 );
	//	CreateEnemy_Road06_4( [CenX,MinY-64] );
		while( 1 ){ yield; }
	}
	//*/
	
	task E_Pattern_Road06_3A( a ){
		loop( a ){
			CreateEnemy_Road06_3( [MinX-64,MinY-32+32*cos(frame*21)], [CenX+80+80*cos(frame*41),CenY-144+48*cos(frame*35)], [MaxX+256,MinY] );
			loop( 5 ){ yield; }
		}
	}

	task E_Pattern_Road06_3B( a ){
		loop( a ){
			CreateEnemy_Road06_3( [MaxX+64,MinY-32+32*cos(frame*21)], [CenX-80-80*cos(frame*41),CenY-144+48*cos(frame*35)], [MinX-256,MinY] );
			loop( 5 ){ yield; }
		}
	}
	
	
	Stage06_WorldManager();
	DeleteMusic();
	PlayMusic( 11, true, 0 );
	if( IsOverDrive ){
		BGM_Stop;
		task BGM_Stop{
			loop( 50 ){
				StopMusic;
				yield;
			}
		}
	}
	else{
		//　中ボスロード開始
		compile_file_h[0] = LoadScriptInThread( plural[0] );
		LoadBossScript( compile_file_h[0] );
		
		FrameWait( 60 );
		
		//　引数2に情報セット　ｲﾍﾞﾝﾄ側でその情報を取り出す
		//　[ テクスチャ番号(0~2), 画像rect番号 ]
		NotifyEventAll( EV_USER + 2, [ 1, 4 ] );
		
		
		FrameWait( 80 );
		loop( 6 ){
			CreateEnemy_Road06_1( [CenX-60,MinY-64] );
			CreateEnemy_Road06_1( [CenX+60,MinY-64] );
			FrameWait( 50 );
		}
		E_Pattern_Road06_3A( 8 );
		FrameWait( 90 );
		E_Pattern_Road06_3B( 8 );
		FrameWait( 320 );
		
		StageTitleDisplay(180, csd ~ "./img/stg/stg6img/stg_t06a.png", [128,64,32] );
		FrameWait( 300 );
		
		CreateEnemy_Road06_2( [CenX-128,MinY-64], [MaxX+128,CenY], [512,128], 240, -1 );
		E_Pattern_Road06_3A( 8 );
		FrameWait( 220 );
		loop( 5 ){
			CreateEnemy_Road06_1( [CenX-96,MinY-64] );
			FrameWait( 45 );
		}
		FrameWait( 180 );
		CreateEnemy_Road06_2( [MaxX+64,MinY+64], [MinX-32,CenY-64], [512,190], 240, -0.8 );
		FrameWait( 200 );
		loop( 5 ){
			CreateEnemy_Road06_1( [CenX+96,MinY-64] );
			FrameWait( 45 );
		}
		FrameWait( 140 );
		CreateEnemy_Road06_2( [MinX-64,MinY+112], [MaxX-48,MinY], [320,400], 240, -0.8 );
		FrameWait( 200 );
		E_Pattern_Road06_3A( 10 );
		FrameWait( 300 );
		CreateEnemy_Road06_2( [MinX,MinY-48], [MaxX-64,MaxY], [320,400], 0, 0.5 );
		FrameWait( 100 );
		E_Pattern_Road06_3B( 10 );
		FrameWait( 340 );
		CreateEnemy_Road06_2( [MinX-32,MinY+112], [MaxX,MinY+48], [300,300], 270, 0.5 );
		FrameWait( 160 );
		CreateEnemy_Road06_2( [MaxX+32,MinY+112], [MinX+48,MinY+80], [500,200], 180, 0.5 );
		FrameWait( 600 );
		CreateEnemy_Road06_4( [ CenX, MinY-64 ] );
		
		FrameWait( 1000 );
		
		//　中ボス
		VanishAllEnemy();
		WaitForSpell();
		DeleteShotAll(TYPE_ALL, TYPE_ITEM);
		FrameWait(60);
		RegistBoss();
		FrameWait(120);
	}
	
	WaitForZeroEnemy();
	//　次ボスロード開始
	compile_file_h[1]	= LoadScriptInThread(plural[1]);
	LoadBossScript(compile_file_h[1]);
	
	ActiveArray_Change02( 0, false );	
	FrameWait( 600 );
	//*/
	//　ボス
	WaitForZeroEnemy();
	VanishAllEnemy();
	WaitForSpell();
	DeleteShotAll(TYPE_ALL, TYPE_ITEM);
	FrameWait(60);
	RegistBoss();
	FrameWait(300);
	WaitForZeroEnemy();
	FrameWait(120);
	
	//　ステージ背景変更奴の添え字初期化
	ActiveArray_Default( true );
	
	//----------------敵の作成--------------------------------

	task CreateEnemy_Road06_1(posD){
		let enm_obj = CreateEnemy01_Onmyougyoku( "YELLOW", 20, 0, 100, 100, 0, 30 );
		
		OutDelete( enm_obj, [128,128] );
		
		ObjMove_SetPosition( enm_obj, posD[0], posD[1] );
		Road06_ShotPattern01( enm_obj, BGW_YELLOW_21 );
		loop( 170 ){ yield; }
		let r = 1;
		if( posD[0]<CenX ){ r = -r; }
		while( !Obj_IsDeleted( enm_obj ) ){
			let ag = 90 + 36 * r;
			let v = 64;
			let mx = ObjMove_GetX( enm_obj ) + v * cos( ag );
			let my = ObjMove_GetY( enm_obj ) + v * sin( ag );
			let spd = v / 24;
			ObjMove_SetDestAtWeight( enm_obj, mx, my, 24, spd );
			loop( 30 ){ yield; }
			r = -r;
		}
	}

	task CreateEnemy_Road06_2(posD,posM,dis,angle,spin){
		let enm_obj = CreateEnemy01_Fairy( 7, 0, 172, 0, 100, 1000, 96 );
		OutDelete( enm_obj, [128,128] );
		ObjMove_SetPosition( enm_obj, posD[0], posD[1] );
		ObjMove_SetDestAtWeight_Circle( enm_obj, posM, dis, angle, spin, 48, 2, 0, 2 );
		Road06_ShotPattern02A( enm_obj, BGB_RED_08 );
		let wt = 16;
		loop( 16 ){
			CallSlave( enm_obj, wt );
			wt += 16;
		}
		task CallSlave( master, wt ){
			let slave_obj = CreateEnemy01_Soul( "BLUE", 12, 0, 100, 20, 16 );
			let master_life = 1;
			let pos = [ ObjMove_GetX( slave_obj ), ObjMove_GetY( slave_obj ) ];
			OutDelete( slave_obj, [128,128] );
			ObjMove_SetPosition( slave_obj, posD[0], posD[1] );
			Road06_ShotPattern02B( slave_obj, BGB_BLUE_04 );
			while( !Obj_IsDeleted( slave_obj ) ){
				if( master_life <= 0 ){
					loop( wt/4 ){ yield; }
					if( Obj_IsDeleted( slave_obj ) ){ return; }
					Obj_Delete( slave_obj );
					//通常とは違う撃破処理（点符微量）
					let data = [ pos[0], pos[1], 2 ];
					NotifyEventAll( EV_USER + 35, data );
					ObjSound_Play( UtilitySE[7] );
					return;
				}
				if( !Obj_IsDeleted( master ) ){
					master_life = ObjEnemy_GetInfo( master, INFO_LIFE );
					SlaveMove( slave_obj, wt );
				}
				ObjMove_SetPosition( slave_obj, pos[0], pos[1] );
				yield;
			}
			let spoil_obj = CreateEnemy01_Soul( "RED", 9999, 0, 0, 0, 0 );
			Obj_SetValue( spoil_obj, "Collision_Player", 0 );
			OutDelete( spoil_obj, [128,128] );
			ObjMove_SetPosition( spoil_obj, pos[0], pos[1] );
			Road06_ShotPattern02C( spoil_obj, ADD_BGW_RED_02 );
			while( !Obj_IsDeleted( spoil_obj ) ){
				if( master_life <= 0 ){
					loop( wt/4 ){ yield; }
					if( Obj_IsDeleted( spoil_obj ) ){ return; }
					Obj_Delete( spoil_obj );
					let drop = 20;
					if( GetAreaCommonData("MegaEvolution", "Frag", false ) ){
						drop *= 2;
					}
					//通常とは違う撃破処理（点符増）
					let data = [ pos[0], pos[1], drop ];
					NotifyEventAll( EV_USER + 35, data );
					ObjSound_Play( UtilitySE[7] );
					return;
				}
				if( !Obj_IsDeleted( master ) ){
					master_life = ObjEnemy_GetInfo( master, INFO_LIFE );
					SlaveMove( spoil_obj, wt );
				}
				ObjMove_SetPosition( spoil_obj, pos[0], pos[1] );
				yield;
			}
			
			task SlaveMove( obj, wt ){
				let pos_m = [ ObjMove_GetX( master ), ObjMove_GetY( master ) ];
				loop( wt ){
					yield;
				}
				let pos_b = [ ObjMove_GetX( obj ), ObjMove_GetY( obj ) ];
				let angle = atan2( pos_m[1] - pos_b[1], pos_m[0] - pos_b[0] );
				ObjMove_SetAngle( obj, angle );
				pos = pos_m;
			}
		}
	}

	task CreateEnemy_Road06_3(posD,posM,posE){
		let enm_obj = CreateEnemy01_Fairy( 1, 0, 18, 10, 100, 100, 7 );
		Obj_SetValue( enm_obj, "Collision_Player", 0 );
		OutDelete( enm_obj, [128,128] );
		
		ObjMove_SetPosition( enm_obj, posD[0], posD[1] );
		ObjMove_SetDestAtWeight_Acceleration( enm_obj, posM, 16, 0, 0.1, 6 );
		Road06_ShotPattern03( enm_obj, BGW_RED_20 );
		loop( 180 ){ yield; }
		ObjMove_SetDestAtWeight_Acceleration( enm_obj, posE, 16, 0, 0.02, 6 );
	}

	task CreateEnemy_Road06_4(posD){
		//let enm_obj = CreateEnemy01_Fairy( 7, 4, 512, 128, 100, 500, 144 );
		let enm_obj = CreateEnemy01_Sakuri( 7, 4, 512, 128, 100, 500, 144 );
		OutDelete( enm_obj, [128,128] );
		RedMagicCircle( enm_obj, 0 );
		State;
		ObjMove_SetPosition( enm_obj, posD[0], posD[1] );
		Road06_ShotPattern04A( enm_obj, BGB_BLUE_04 );
		ObjMove_SetDestAtWeight_Acceleration( enm_obj, [CenX,MinY+96], 16, 0, 0.1, 6 );
		loop( 100 ){ yield; }
		let r = 1;
		loop( 12 ){
			let p = rand( 0,360 );
			loop( 5 ){
				if( Obj_IsDeleted( enm_obj ) ){ return; }
				let angle = p;
				let way = 3;
				loop( way ){
					let slave_obj = CreateEnemy01_Soul( "AQUA", 4, 25, 100, 25, 10 );
					Obj_SetValue( slave_obj, "Collision_Player", 0 );
					OutDelete( slave_obj, [128,128] );
					ObjMove_SetPosition( slave_obj, ObjMove_GetX( enm_obj ), ObjMove_GetY( enm_obj ) );
					Slave_Move( angle, r );
					Road06_ShotPattern04B( slave_obj, BGB_AQUA_08, r );
					ObjSound_Play( CreateShotSE[ 1 ] );
					task Slave_Move( angle, r ){
						let speed = 2;
						while( !Obj_IsDeleted( slave_obj ) ){
							ObjMove_SetSpeed( slave_obj, speed );
							ObjMove_SetAngle( slave_obj, angle );
							speed += 0.01;
							angle -= 0.8 * r;
							yield;
						}
					}
					angle += 360/way;
				}
				p += 10 * r;
				loop( 7 ){ yield; }
			}
			loop( 24 ){ yield; }
			r = -r;
		}
		ObjMove_SetDestAtWeight_Acceleration( enm_obj, [CenX,MinY-256], 16, 0, 0.02, 6 );
		task State{
			let pos = [ ObjMove_GetX( enm_obj ), ObjMove_GetY( enm_obj ) ];
			let life = ObjEnemy_GetInfo( enm_obj, INFO_LIFE );
			while( !Obj_IsDeleted( enm_obj ) ){
				pos = [ ObjMove_GetX( enm_obj ), ObjMove_GetY( enm_obj ) ];
				life = ObjEnemy_GetInfo( enm_obj, INFO_LIFE );
				yield;
			}
			if( life <= 0 ){
				NotifyEventAll( EV_USER + 3, pos );
				loop( 40 ){
					DeleteShotAll(TYPE_SHOT, TYPE_ITEM);
					yield;
				}
			}
		}
	}
	
	//　ユーティリティ
	task OutDelete(ID, dis ){
		let pos	= [ObjMove_GetX(ID),ObjMove_GetY(ID)];
		while(pos[0] < MaxX+dis[0] && pos[0] > MinX-dis[0] && pos[1] < MaxY+dis[1] && pos[1] > MinY-dis[1]){
			if( Obj_IsDeleted(ID) ){ break; }
			pos	= [ObjMove_GetX(ID),ObjMove_GetY(ID)];
			yield;
		}
		Obj_Delete(ID);
	}
}

//　ステージ背景推移マネージャ
task Stage06_WorldManager(){
	let IsActive = GetAreaCommonData("StageTrance", "active", [true,true,true,true] );
	
	SetWorld06a();
	while( IsActive[0] ){
		IsActive = GetAreaCommonData("StageTrance", "active", [true,true,true,true] );
		yield;
	}
	SetWorld06b();
}
